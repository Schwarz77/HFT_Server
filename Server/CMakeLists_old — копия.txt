
find_package(simdjson REQUIRED)
find_package(ZLIB REQUIRED)
find_package(OpenSSL REQUIRED)
find_package(ixwebsocket CONFIG REQUIRED)
add_definitions(-DSIMDJSON_USING_LIBRARY=1)


add_library(ServerCore STATIC 
    RingBuffer.h
    Server.h Server.cpp 
    Session.h Session.cpp
)

target_include_directories(
    ServerCore
    PUBLIC 
        ${CMAKE_CURRENT_SOURCE_DIR}
)

target_link_libraries(
    ServerCore 
    PUBLIC 
        Utils 
        ProjectInclude 
        Boost::system 
        Boost::thread 
        Boost::asio
        Boost::format
    PRIVATE		
		simdjson::simdjson
		ixwebsocket::ixwebsocket
		OpenSSL::SSL
		OpenSSL::Crypto
		ZLIB::ZLIB
		bcrypt
		crypt32
		ws2_32			
)

add_executable(Server main.cpp)

#set_target_properties(Server PROPERTIES VS_STARTUP_PROJECT 1)

target_link_libraries(
    Server
    PRIVATE 
        ServerCore
)

# 2. Флаги оптимизации — КРИТИЧЕСКИ ВАЖНО для Lock-Free и SIMD
if(MSVC)
    # 1. Базовые флаги для всех конфигураций
    target_compile_options(Server PRIVATE /W4 /EHsc /arch:AVX2)

    # 2. Флаги только для Debug (никаких оптимизаций, чтобы работала отладка)
    target_compile_options(Server PRIVATE 
        $<$<CONFIG:Debug>:/Od>  # Отключить оптимизацию
        $<$<CONFIG:Debug>:/Zi>  # Генерировать полные отладочные символы
        $<$<CONFIG:Debug>:/RTC1> # Проверки времени выполнения
    )

    # 3. Флаги только для Release (максимальная скорость)
    target_compile_options(Server PRIVATE 
        $<$<CONFIG:Release>:/Ox> 
        $<$<CONFIG:Release>:/Ot> 
        $<$<CONFIG:Release>:/GL>
    )

    # Чтобы линковщик видел отладочные символы
    set_target_properties(Server PROPERTIES 
        LINK_FLAGS_DEBUG "/DEBUG"
    )
else()
    target_compile_options(Server PRIVATE -Wall -O3 -march=native)
endif()


# Настройка для LTO (Link Time Optimization)
# Позволяет оптимизировать вызовы функций между разными файлами .cpp
include(CheckIPOSupported)
check_ipo_supported(RESULT ipo_supported OUTPUT error)
if(ipo_supported)
    set_target_properties(Server PROPERTIES INTERPROCEDURAL_OPTIMIZATION TRUE)
endif()